apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

version = "1.0.0"

android {
    resourcePrefix "zz_" //随意填写

    compileSdkVersion 24
    buildToolsVersion "24.0.1"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.google.code.gson:gson:2.4'

}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

group = "me.zhouzhuo.zzhorizontalprogressbar"            // Maven Group ID for the artifact，一般填你唯一的包名
install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'
            groupId 'me.zhouzhuo.zzhorizontalprogressbar' //自己定义的组名
            artifactId 'zz-horizontal-progressbar'

            name 'zz-horizontal-progressbar'
            description 'A Horizontal ProgressBar that is customized easily.'
            url 'https://github.com/zhouzhuo810/ZzHorizontalProgressBar'
            inceptionYear '2016'

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection 'https://github.com/zhouzhuo810/ZzHorizontalProgressBar.git'
                url 'https://github.com/zhouzhuo810/ZzHorizontalProgressBar'

            }
            developers {
                developer {
                    name 'zhouzhuo'
                    email 'admin@zhouzhuo.me'
                }
            }
        }
    }
}

// Bintray

//获取local.propertes的信息
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    publish = true
    configurations = ['archives']
    pkg {
        //填入 bintray 上对应的 package 信息
        repo = 'maven'
        name = 'zz-horizontal-progressbar'
        vcsUrl = 'https://github.com/zhouzhuo810/ZzHorizontalProgressBar.git'
        websiteUrl = 'https://github.com/zhouzhuo810/ZzHorizontalProgressBar'
        licenses = ['Apache-2.0']
        issueTrackerUrl = 'https://github.com/zhouzhuo810/ZzHorizontalProgressBar/issues'
        publicDownloadNumbers = true
        version {
            name = '1.0.0'
            desc = 'zz-horizontal-progressbar release'
            vcsTag = '1.0.0'
            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

task findConventions << {
    println project.getConvention()
}
